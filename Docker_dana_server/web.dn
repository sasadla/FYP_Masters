uses ws.forms.FileFormField

//this component is a webservice that talks to the controller and gives details and information on running docker contianers, it further is able to bring down or power up a docker container if the controller issues the command to do so.

//it makes use of bash scripts to perform its functions as it is deployed in a ubuntu envoirnment.
component provides ws.Web requires ws.FormParser fparser, io.Output out, os.SystemInfo info, os.Run run, io.File, data.StringUtil stringUtil,data.IntUtil intUtl,time.DateUtil dtTm, net.http.HTTPRequest rq, data.query.Search search,time.Calendar caln, data.json.JSONParser jparser{
 	
	/**
	*this function is used by the ws.core module of dana to listen on the specified port for incomming get requests
	*
	*inputs:
	*DocStream s stream where the request came from and reply is to be served to
	*char path[] the path of the webservices i.e localhost:2000/index here /index is the path
	*
	*result:
	*a char array that containes the result of the performed command 
	*/
	
	bool Web:get(char path[], DocStream s)
	{
		
		char pth[] = "/home/lancs/sadla/FYP_Masters/Docker_dana_server"
		char log[]
		RunStatus ssst = run.execute("./clear_file.bash iam.txt")
		ssst = run.execute("hostname >> iam.txt")
		
		char av_host[] =stringUtil.explode(read_file("iam.txt"),"\n\r")[0].string
		
		out.println(path)
		if (path == "/controller_table_log.txt")//teste the container for replytimes and gets its stats
		{	
			ssst = run.execute("./clear_file.bash check.txt")
			ssst = run.execute("docker ps -q -a >> check.txt")
			char ch[] = read_file("check.txt")
			
			if(ch.arrayLength <= 0)
			{
				log = "Webservice down Cannot run test"
			}
			else
			{
				log = run_log_gen(pth)
				out.println("Sent back: $log")
			}
		}
		else if (path == "/WT_controller_table_log.txt")//returns back information on the container without running the test
		{
			log = read_file("controller_table_log.txt")
			out.println("Sent back: $log")
		}
		else if (path == "/down")//checks to make sure if a container exists if it does powers it down else returns saying conatiner already down
		{
			ssst = run.execute("./clear_file.bash check.txt")
			ssst = run.execute("docker ps -q -a >> check.txt")
			char chtr[] = read_file("check.txt")
			
			if(chtr.arrayLength > 0)
			{
				RunStatus st = run.execute("./down_script.bash")
				log = "Webservice on $av_host Down"
				out.println("Sent back: $log")
			}
			else
			{
				log = "Webservice on $av_host already Down"
				RunStatus sts = run.execute("echo d_down_success >> down_res.txt")
				out.println("Sent back: $log")
			}
		}
		else if (path == "/up")//checks to make sure if a container exists if it does not powers it up else returns saying conatiner already up
		{	
			ssst = run.execute("./clear_file.bash check.txt")
			ssst = run.execute("docker ps -q -a >> check.txt")
			char cht[] = read_file("check.txt")
			
			if(cht.arrayLength <= 0)
			{
				RunStatus st = run.execute("./up_container.bash")
				log = "Webservice on $av_host Up\n$(run_log_gen(pth))"
				out.println("Sent back: $log")
			}
			else
			{
				char a[] = read_file("controller_table_log.txt")
				log = "Webservice on $av_host already Up\n$(a)"
				out.println("Sent back: $log")
			}
		}
		else if (path == "/down_res.txt")//returns container power up/down status file
		{
			log = read_file("down_res.txt")
			out.println("Sent back: $log")
		}
		s.write(log)//string concatination done here
		
		return true
	}

	/**
	*this function is used by the ws.core module of dana to listen on the specified port for incomming post requests
	*
	*inputs:
	*DocStream s stream where the request came from and reply is to be served to
	*char path[] the path of the webservices i.e localhost:2000/index here /index is the path
	*char contentType[] the type of content to post
	*byte content[] the content itself
	*result:
	*a char array that containes the result of the performed command 
	*/
	
	
	bool Web:post(char path[], char contentType[], byte content[], DocStream stream)
	{
		return true
	}
		
	
	/**
	*this function runs test on the running container to get its id the host thats running its ip it its ip its minimum reply time and average time and a thread count i.e the load on it 
	*
	*inputs:
	*char p[] the path where the resultant logs are to be generated and kept
	*
	*result:
	*a char array that containes the result of the performed test 
	*/
	
	
	char [] run_log_gen(char p[])
	{
	
		String ids[] = get_cntnr_id("ids_res.txt",p)
		int cntr=0
		char log_path_ins[]="inspect_res.txt"
		char gt[] = "Gateway"
		char i_p[]= "IPAddress"
		
		int tCount=1
		
		int final_avg
		char c_details[]
		
		char stats[] = get_status("status_res.txt",p)
		RunStatus s = run.execute("./clear_file.bash controller_container_stats_log.txt")
		while (cntr < ids.arrayLength)
		{
			if (ids[cntr]==null){
			}
			else{
				char id[]=ids[cntr].string
				char path[]="./d_inspect.bash $id"
				s = run.execute(path)
	
				write_to_file("controller_container_stats_log.txt","Container id is:$id")
				write_to_file("controller_container_stats_log.txt","Gateway is:$(extract_info_on_container(log_path_ins,gt))")
				write_to_file("controller_container_stats_log.txt","Ip address is:$(extract_info_on_container(log_path_ins,i_p))")
			}
			cntr++
		}
		
		c_details = read_file("controller_container_stats_log.txt")
				
		final_avg = start_test(tCount,p)
		gen_status_table(stats,c_details,tCount,p)
	
		return read_file("controller_table_log.txt")
	}
	
	/**
	*After the test is run this function is called to generate alog file with the extracted details on the conatiner and store them on the disk
	*
	*inputs:
	*char stats[] the detailed information of the running container 
	*char c_details[] extracetd conatiner id, hostname and ip of the container
	*int t_count the load that was put on the container to generate the reply times i.e 1 means 1000ms delay
	*char p[] path to where the log is to be stored
	*
	*result:
	*log file stored in given location with the above mentioned information 
	*/
	
	
	void gen_status_table(char stats[],char c_details[],int tCount, char p[])
	{
		// three log files:
		//controller_container_stats_log.txt containerid,ip,gateway (c_details)
		//controller_ttr_log.txt //min avg max avg ttr
		//status_res.txt host details here (stats)
		
		char log_name[] ="controller_table_log.txt"
		
		RunStatus s = run.execute("./clear_file.bash $log_name")
		//5,6
		char host_ident[] = stringUtil.explode(stringUtil.explode(stats, ":")[1].string,"\n\r")[0].string
		
		char c_id[] = stringUtil.explode(stringUtil.explode(c_details, ":")[1].string,"\n\r")[0].string
		char c_ip_on_host[] = stringUtil.explode(stringUtil.explode(c_details, ":")[3].string,"\n\r")[0].string
		
		char avg_ttr_w_srvc[] = stringUtil.explode(stringUtil.explode(read_file("controller_ttr_log.txt"), "\n\r")[tCount].string,":")[1].string
		char min_avg_ttr_w_srvc[] = stringUtil.explode(stringUtil.explode(read_file("controller_ttr_log.txt"), "\n\r")[tCount+1].string,":")[1].string
		
		
		write_to_file(log_name,"Host identity:$host_ident")
		write_to_file(log_name,"Container id:$c_id")
		write_to_file(log_name,"Container ip on host:$c_ip_on_host")
		write_to_file(log_name,"On Load Average TTR webservice:$avg_ttr_w_srvc")
		write_to_file(log_name,"Minimum Average TTR webservice:$min_avg_ttr_w_srvc")
		write_to_file(log_name,"Thread count:$(intUtl.intToString(tCount))")
		
	}
	
	/**
	*this function runs the test on the container and genrates logs that are later used to generate min reply time and average reply time
	*
	*inputs:
	*int tcount the load to put on the conatiner while testing i.e 1 = 1000ms delay in reply time
	*char p[] the path of where the logs should be temporarily kept
	*
	*result:
	*an int with the average reply time for the container 
	*/
	
 
	int start_test(int tcount,char p[])
	{
	
		int req_num = 10//numer of requestes to be made by one thread
		int threadCount = tcount
		Thread myThreads[] = new Thread[threadCount]
		String urls[] = stringUtil.explode(read_file("tst_urls_log.txt"), "\n\r")//only put 5 links in this file otherwise system will crash
		
		out.println("Getting Minimum reply time for web service")
		
		int min_rep_avg = get_min_reply_avg_time(urls,p)
		
		out.println("\nStarting time to reply test for web service with $(intUtl.intToString(threadCount)) threads\nMaking Requests now")
		for (int i = 0; i < threadCount; i++)
		{	
			char url[]
			if (i < urls.arrayLength)
			{
				url = urls[i].string
			}
			else
			{
				url = urls[3].string
			}
			//out.println(url)
			myThreads[i] = asynch::rqst(url, i, req_num,p)
			
		}
		
		for (int i = 0; i < threadCount; i++)
		{
			myThreads[i].join()
			
		}
		out.println("\nTest Complete results are:")
		
		return gen_time_avgs_log(threadCount,req_num,min_rep_avg,p)
		
	
	}
	
	/**
	*this functions launches threads to test the conatiner running the webservice for it minimum reaply average time
	*
	*inputs:
	*String urls[] urls to test on the webservice
	*char p[] the path where the logs should be kept temporarily
	*
	*result:
	*an int with the minimum reply time result of the tests 
	*/
	
	
	int get_min_reply_avg_time(String urls[],char p[])
	{
		int min_avg = 0
		for (int i = 0; i < 5; i++)
		{	
			char url[]
			if (urls[i]!= null)
			{
				url = urls[i].string
			}
			//out.println(url)
			min_avg = min_avg + rqst(url, 0, 1,p)
			
		}
		min_avg = min_avg/5
		//out.println("Minimum average reply time for web service test is: $(intUtl.intToString(min_avg)) ms")
		return min_avg
	}
 
	/**
	*this functions launches threads to test the conatiner running the webservice for its reply average time
	*
	*inputs:
	*int threadCount number of threads to use to test the services
	*int req_num number of requests each thread should make
	*int min_rep_avg minimum average reply time of cainter to write in the log
	*char p[] pathe where the log is to be stored
	*
	*result:
	*an int with the average reply time result of the tests 
	*/
	
	
	int gen_time_avgs_log(int threadCount,int req_num,int min_rep_avg,char p[])
	{	
		int avg_tme[] = new int [threadCount]
		int final_avg = 0
		char log_name[] = "controller_ttr_log.txt"
		RunStatus s = run.execute("./clear_file.bash $log_name")
		for (int i = 0; i < threadCount; i++)
		{
			char log_name_T[] = "controller_log_$(intUtl.intToString(i)).txt"
			char res[] = read_file(log_name_T)
			char url[] = stringUtil.explode(res, "\n\r")[0].string
			
			url = new char[](stringUtil.explode(res, ":")[1].string,":",stringUtil.explode(res, ":")[2].string,":",stringUtil.explode(stringUtil.explode(res, ":")[3].string,"\n\r")[0].string)
			
			avg_tme[i] =intUtl.intFromString(stringUtil.explode((stringUtil.explode(res, "\n\r")[req_num+1].string), ":")[1].string)
			final_avg = final_avg + avg_tme[i]
			write_to_file(log_name,"Average reply time for thread number $(intUtl.intToString(i+1)) testing the url $url is:$(intUtl.intToString(avg_tme[i])) ms")
			s = run.execute("rm -r -f $log_name_T")
		}
		final_avg = final_avg/ threadCount
		write_to_file(log_name,"Final average reply time for web service test is:$(intUtl.intToString(final_avg)) ms")
		write_to_file(log_name,"Minimum average reply time for web service test is:$(intUtl.intToString(min_rep_avg)) ms")
		out.println(read_file(log_name))
		return final_avg
	}
	
	/**
	*this gets the host name and the container id and the status of the running container
	*
	*inputs:
	*char f_n[] name of file to read to extract status
	*char p[] the path where the logs should be kept
	*
	*result:
	*a char array with the result of the status test in it 
	*/
	
	
	
	char[] get_status(char f_n[],char p[])
	{
		RunStatus s = run.execute("./live_container_info.bash")
						
		char res[] = read_file(f_n)
		
		return res
	
	}
	
	/**
	*this functions gets the id(s) of the running container(s)
	*
	*inputs:
	*char f_n[] file where the ids result is read from
	*char p[] the path where the logs should be kept
	*
	*result:
	*A string array with the collected ids result in it 
	*/
	
	
	String[] get_cntnr_id(char f_n[],char p[])
	{
		RunStatus s = run.execute("./get_ids.bash")
						
		char res[] = read_file(f_n)
		String cntnr_id[]
		
		cntnr_id = stringUtil.explode(res, "\n\r")
			
		return cntnr_id
	
	}
	
	/**
	*this functions read a json file that has been given as a result of getting the running containers detailed configs
	*
	*inputs:
	*char f_n[] where the detailed reult is to be read from
	*char inf[] the specific information required from the file
	*
	*result:
	*a char array that containes the specifed result frm the json file 
	*/
	
	char[] extract_info_on_container(char f_n[],char inf[])
	{
		char infrm[]
		
		char jsonArray[] = new char[]("{ \"array\":", read_file(f_n), "}")
		JSONElement doc = jparser.parseDocument(jsonArray)
		
		JSONElement cellA = jparser.getValue(doc, "array").children[0]
	
		JSONElement leaf = jparser.getValue(cellA, "NetworkSettings")
		
		leaf = jparser.getValue(leaf, "Networks")
		
		leaf = jparser.getValue(leaf, "docker_dana_server_default")
		
		infrm = jparser.getValue(leaf, inf).value
		
		return infrm
	
	}
	
	/**
	*this functions write to a file in the same directory as the web.dn file
	*
	*inputs:
	*char f_n[] name of file to write to
	*char d[] data to be written onto the file
	*
	*result:
	*specified file with the given data in the same directory as the web.dn file  
	*/
	
	void write_to_file(char f_n[],char d[])
	{	
		File f = new File(f_n, File.FILE_ACCESS_WRITE)
		f.setPos(f.getSize())
		f.write("$d\n")
		f.close()
	}
	
	/**
	*this functions reads from a file in the same directory as the web.dn file
	*
	*inputs:
	*char f_n[] name of file to read from
	*
	*result:
	*char array with the contents of the file in it  
	*/
	
	char[] read_file(char f_name[])
	{
		File f = new File(f_name, File.FILE_ACCESS_READ)
		char q[] = f.read(f.getSize())
		f.close()
		return q
		
	}
	
	
	/**
	*this functions is used by the average time collection threads to make a given number of requests to the webservice and returns back the average time for those requests
	*
	*inputs:
	*char urls[] urls to make the requests to
	*int cnt thread number to use to assign the log file a unique name
	*int reqno number of requests to be made by the file
	*char p[] path where the log files generated are to be stored
	*
	*result:
	*specified file with the given data in the same directory as the web.dn file  
	*/
	
	int rqst(char url[],int cnt, int reqno,char p[])
	{
		char log_name[] = "controller_log_$(intUtl.intToString(cnt)).txt"
		RunStatus s = run.execute("./clear_file.bash $log_name")
		int tme[] = new int [reqno]
		int avg = 0
		write_to_file(log_name,"This log is for URL :$url")
		for (int i = 0; i < reqno; i++)
		{
			DateTime st = caln.getTime()
			HTTPResponse r = rq.get(url, null)/*"http://127.0.0.1:5100"*/
			
			DateTime end = caln.getTime()
			
			DateTime dif = dtTm.diff(st, end)
			
			int e_t =dtTm.toMilliseconds(dif)
			tme[i]= e_t
			//char resp[] =r.content
			//out.println("Request number: $(intUtl.intToString(i+1)) made to: $url with response time: $(intUtl.intToString(e_t)) ms")
			out.print("-")
			
			write_to_file(log_name,"Request number: $(intUtl.intToString(i+1)) made to: $url with response time: $(intUtl.intToString(e_t)) ms")
		}
		
		for (int x = 0; x < reqno; x++)
		{
			avg = avg + tme[x]
			
		}
		avg = avg/reqno
		write_to_file(log_name,"Average for thread number $(intUtl.intToString(cnt+1)):$(intUtl.intToString(avg))")
		//out.println("res is: $resp")
		return avg
	}
	
}
